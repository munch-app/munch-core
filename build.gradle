buildscript {
    repositories {
        maven {
            url 'http://fx.artifactory.munchapp.co.fuxing.dev/artifactory/munch-repos'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

plugins {
    id "com.bmuschko.docker-java-application" version "4.6.2"
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: "com.jfrog.artifactory"
    apply plugin: 'maven-publish'

    group 'app.munch'
    version '0.24.0'
    ext.dockerVersion = '0.24.0'

    ext.jUnitVersion = '5.4.2'
    ext.guiceVersion = '4.2.2'
    ext.awsVersion1 = '1.11.620'
    ext.awsVersion2 = '2.7.21'

    ext.transportVersion = '1.0-SNAPSHOT'

    // Deprecating START
    ext.dockerRegistryUrl = '197547471367.dkr.ecr.ap-southeast-1.amazonaws.com/'
    ext.catalystVersion = '3.4-SNAPSHOT'
    ext.restfulVersion = '3.0.9'
    ext.fileVersion = '1.2.17'
    ext.locationVersion = '1.2.1'
    ext.userVersion = '3.2.4'
    ext.notificationVersion = '1.5'
    ext.dataVersion = '5.0-SNAPSHOT'
    ext.tasteVersion = '1.0-SNAPSHOT'
    ext.suggestVersion = '2.0-SNAPSHOT'
    ext.feedVersion = '2.0-SNAPSHOT'
    ext.instagramVersion = '1.0-SNAPSHOT'
    ext.articleVersion = '1.0-SNAPSHOT'
    // Deprecating END

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact(sourcesJar) {
                    classifier = 'sources'
                }
            }
        }
    }

    configurations.all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }

    dependencies {
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: jUnitVersion
        testCompile group: 'org.easytesting', name: 'fest-assert', version: '1.4'

        compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1u2'
    }
}

// https://www.jfrog.com/confluence/display/RTF/Gradle+Artifactory+Plugin
artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = "munch-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'munch-repos'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}
