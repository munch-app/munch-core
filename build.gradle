allprojects {
    apply plugin: 'java'
    apply plugin: "com.jfrog.artifactory"
    apply plugin: 'maven-publish'

    group 'com.munch.core'
    version '0.10.7'
}

buildscript {
    repositories {
        maven {
            url 'http://artifactory.fxloh.com/artifactory/plugins-release'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }

    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'munch'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(sourcesJar) {
                classifier = 'sources'
            }
        }
    }
}

repositories {
    maven {
        credentials {
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
        url "http://artifactory.fxloh.com/artifactory/munch"
    }
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.easytesting:fest-assert:1.4'

    // Debugging
    compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1u2'

    // Basic
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.7' // Logging
    compile group: 'com.spatial4j', name: 'spatial4j', version: '0.5' // GeoSpatial Util
    compile group: 'de.svenkubiak', name: 'jBCrypt', version: '0.4.1' // Password Util Library
    compile group: 'com.google.inject', name: 'guice', version: '4.0' // Injection Library Default as of 0.5

    // Apache
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'commons-configuration', name: 'commons-configuration', version: '1.10'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2.1'
    compile group: 'org.apache.tika', name: 'tika-core', version: '1.13'

    // AWS
    compile group: 'com.amazonaws', name: 'aws-java-sdk-dynamodb', version: '1.11.26'
    compile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.26'

    // Block
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.3'
}
