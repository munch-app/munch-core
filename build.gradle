allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: "com.jfrog.artifactory"
    apply plugin: 'maven-publish'

    group 'munch-core'
    version '0.22.0'

    ext.dockerRegistryUrl = '197547471367.dkr.ecr.ap-southeast-1.amazonaws.com/'

    ext.jUnitVersion = '5.3.2'
    ext.guiceVersion = '4.2.2'
    ext.awsVersion = '1.11.508'

    ext.catalystVersion = '3.4-SNAPSHOT'

    ext.restfulVersion = '3.0.9'
    ext.fileVersion = '1.2.17'
    ext.locationVersion = '1.2.1'

    ext.userVersion = '3.2.4'
    ext.notificationVersion = '1.5'

    ext.dataVersion = '5.0-SNAPSHOT'

    ext.suggestVersion = '2.0-SNAPSHOT'
    ext.feedVersion = '2.0-SNAPSHOT'
    ext.instagramVersion = '1.0-SNAPSHOT'
    ext.articleVersion = '1.0-SNAPSHOT'

    sourceCompatibility = 1.9
    targetCompatibility = 1.9
    compileJava.options.encoding = 'UTF-8'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact(sourcesJar) {
                    classifier = 'sources'
                }
            }
        }
    }

    configurations.all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }

    dependencies {
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: jUnitVersion
        testCompile group: 'org.easytesting', name: 'fest-assert', version: '1.4'

        compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1u2'

        compile group: 'munch.restful', name: 'restful-core', version: restfulVersion
        compile group: 'com.google.inject', name: 'guice', version: guiceVersion
    }
}

buildscript {
    repositories {
        maven {
            url 'http://fx.artifactory.munchapp.co.fuxing.dev/artifactory/munch-repos'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
        classpath 'com.bmuschko:gradle-docker-plugin:3.6.2'
    }
}

// https://www.jfrog.com/confluence/display/RTF/Gradle+Artifactory+Plugin
artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = "munch-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'munch-repos'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}
