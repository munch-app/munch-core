allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: "com.jfrog.artifactory"
    apply plugin: 'maven-publish'

    group 'munch-core'
    version '0.14.0'

    ext.dockerRegistryUrl = '197547471367.dkr.ecr.ap-southeast-1.amazonaws.com/'

    ext.jUnitVersion = '5.2.0'
    ext.guiceVersion = '4.2.1'
    ext.awsVersion = '1.11.367'

    ext.catalystVersion = '3.1'

    ext.restfulVersion = '2.4.21'
    ext.fileVersion = '1.2.15'
    ext.locationVersion = '1.1.3'
    
    ext.userVersion = '1.27'
    ext.notificationVersion = '1.5'

    ext.dataVersion = '4.10-SNAPSHOT'
    ext.feedVersion = '1.0-SNAPSHOT'
    ext.instagramVersion = '1.0-SNAPSHOT'
    ext.articleVersion = '1.0-SNAPSHOT'

    sourceCompatibility = 1.9
    targetCompatibility = 1.9
    compileJava.options.encoding = 'UTF-8'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact(sourcesJar) {
                    classifier = 'sources'
                }
            }
        }
    }

    configurations.all { resolutionStrategy.cacheChangingModulesFor 0, 'seconds' }

    dependencies {
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion
        testCompile group: 'org.easytesting', name: 'fest-assert', version: '1.4'

        compile group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1u2'
    }
}

buildscript {
    repositories {
        maven {
            url 'http://fx.artifactory.munchapp.co/artifactory/munch-repos'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.12'
    }
}

// https://www.jfrog.com/confluence/display/RTF/Gradle+Artifactory+Plugin
artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = "munch-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'munch-repos'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}