allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: "com.jfrog.artifactory"
    apply plugin: 'maven-publish'

    group 'com.munch.core'
    version '0.29.1'

    repositories {
        maven {
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
            url "http://artifactory.fxloh.com/artifactory/munch"
        }
        mavenCentral()
    }
}

ext.hibernateVersion = '5.0.10.Final'

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.easytesting:fest-assert:1.4'

    // Munch
    compile group: 'com.munch.core', name: 'essential', version: '0.10.5'

    // Database
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.39'
    compile group: 'org.hibernate', name: 'hibernate-core', version: hibernateVersion
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: hibernateVersion
    compile group: 'org.hibernate', name: 'hibernate-hikaricp', version: hibernateVersion

    // Search
    compile group: 'org.hibernate', name: 'hibernate-search-orm', version: '5.5.4.Final'
}

buildscript {
    repositories {
        maven {
            url 'http://artifactory.fxloh.com/artifactory/plugins-release'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }

    }
    dependencies {
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
    publish {
        repository {
            repoKey = 'munch'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
        defaults {
            publications('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true

        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(sourcesJar) {
                classifier = 'sources'
            }
        }
    }
}