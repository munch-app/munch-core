dependencies {
    compile project(':restful-server')
    compile project(':restful-client')
    
    compile project(':munch-struct')
    compile project(':munch-document')
    compile project(':munch-search')

    // Injection
    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'
    compile group: 'com.google.inject.extensions', name: 'guice-multibindings', version: '4.1.0'

    // For Mvp Testing
    compile group: 'com.thedeanda', name: 'lorem', version: '2.1'
}

apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

mainClassName = 'munch.api.ApiServer'

dockerDistTar {
    runCommand('apk add --update bash && rm -rf /var/cache/apk/*')
}

ext.dockerTag = 'munch-core/munch-api:latest'
ext.dockerHost = '197547471367.dkr.ecr.ap-southeast-1.amazonaws.com/'

docker {
    javaApplication {
        baseImage = 'openjdk:8-jdk-alpine'
        maintainer = 'Fuxing'
        ports = [8088]
        tag = dockerTag
    }
}

task publishDocker {
    dependsOn 'dockerBuildImage'

    doLast {
        exec { commandLine "docker", "tag", dockerTag, dockerHost + dockerTag }
        exec { commandLine "docker", "push", dockerHost + dockerTag }
    }
}

