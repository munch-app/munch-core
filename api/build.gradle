project('api-core') {
    dependencies {
        // Frameworks & Tools
        compile group: 'com.google.inject', name: 'guice', version: guiceVersion

        // Munch Restful
        compile group: 'munch.restful', name: 'restful-client', version: restfulVersion
        compile group: 'munch.restful', name: 'restful-server', version: restfulVersion
        compile group: 'munch.restful', name: 'restful-server-jwt', version: restfulVersion
        compile group: 'munch.restful', name: 'restful-server-firebase', version: restfulVersion

        // Core Clients
        compile group: 'munch-data', name: 'data-client', version: '4.10-SNAPSHOT'
        compile group: 'munch-user', name: 'data-client', version: '1.21'

        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: jUnitVersion
    }
}

configure(subprojects.findAll { !it.name.matches("api-core") }) {
    dependencies {
        compile project(':api:api-core')
        testCompile project(':api:api-core').sourceSets.test.output
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: jUnitVersion
    }
}

project('api-places') {
    dependencies {
        compile group: 'instagram-system', name: 'data-client', version: instagramVersion
        compile group: 'catalyst', name: 'link-client', version: catalystVersion
        compile group: 'catalyst', name: 'edit-website-utils', version: catalystVersion

        // Deprecation Required for all 4
        compile group: 'munch.data', name: 'data-client', version: '3.0.0-SNAPSHOT'
        compile group: 'munch.article', name: 'client-article', version: '1.3.0-SNAPSHOT'
    }
}

project('api-search') {
    dependencies {
        compile group: 'catalyst', name: 'edit-core-utils', version: catalystVersion
    }
}

project('api-landing') {
    dependencies {
        compile project(':api:api-users')
    }
}


project('api-users') {
    dependencies {
        compile group: 'munch-notification', name: 'client', version: '1.5'
    }
}

dependencies {
    compile group: 'com.amazonaws', name: 'aws-java-sdk-ssm', version: awsVersion

    compile project(':api:api-core')
    compile project(':api:api-users')
    compile project(':api:api-places')
    compile project(':api:api-search')
    compile project(':api:api-landing')
}

apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

ext.dockerTag = dockerRegistryUrl + rootProject.name + '/' + project.name + ':'

mainClassName = 'munch.api.ApiModule'

docker {
    javaApplication {
        baseImage = 'openjdk:9-jdk-slim'
        maintainer = 'Fuxing'
        ports = [8000]
        tag = dockerTag + version
    }
}

task buildImage {
    dependsOn 'clean'
    dependsOn 'dockerBuildImage'
    tasks.findByName('dockerBuildImage').mustRunAfter 'clean'
}

task publishApi {
    dependsOn 'buildImage'

    doLast {
        exec { commandLine "docker", "push", dockerTag + version }
    }
}