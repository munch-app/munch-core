ext.hibernateVersion = '5.0.11.Final'

dependencies {
    compile rootProject
    compile project(':restful-server')

    // Injection
    compile group: 'com.google.inject', name: 'guice', version: '4.1.0'

    // Data
    compile group: 'com.munch', name: 'hibernate-utils', version: '5.0.11.Final.1'
    compile group: 'org.hibernate', name: 'hibernate-core', version: hibernateVersion
    compile group: 'org.hibernate', name: 'hibernate-entitymanager', version: hibernateVersion
    compile group: 'org.hibernate', name: 'hibernate-hikaricp', version: hibernateVersion
    compile group: 'org.postgresql', name: 'postgresql', version: '9.4.1212.jre7'

    // Search
    compile group: 'org.elasticsearch.client', name: 'rest', version: '5.2.2'
}

apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

mainClassName = 'munch.data.DataApi'

dockerDistTar {
    runCommand('apk add --update bash && rm -rf /var/cache/apk/*')
}

ext.dockerTag = 'munch-core/munch-service-data:latest'
ext.dockerHost = '197547471367.dkr.ecr.ap-southeast-1.amazonaws.com/'

docker {
    javaApplication {
        baseImage = 'openjdk:8-jdk-alpine'
        maintainer = 'Fuxing'
        ports = [8088]
        tag = dockerTag
    }
}

task publishDocker {
    dependsOn 'dockerBuildImage'

    doLast {
        exec { commandLine "docker", "tag", dockerTag, dockerHost + dockerTag }
        exec { commandLine "docker", "push", dockerHost + dockerTag }
    }
}
